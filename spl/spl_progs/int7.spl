alias physicalSP S0;
physicalSP = ([PTBR + 2*(SP/512)]*512) + (SP%512);

alias sysCallNo S1;
sysCallNo = [physicalSP - 1];

breakpoint;

if (sysCallNo == 10) then
	alias currentPID S2;
	currentPID=(PTBR - 1024)/8;

	alias currentPCB S3;
	currentPCB=READY_LIST + 32*currentPID;

	alias newPID S4;
	newPID = 0;
	alias newPCB S5;
	while(newPID < 32) do
		newPCB = READY_LIST + 32*newPID;
		if ([newPCB + 1] == (100 + currentPID)) then
			[newPCB + 1]=1;
		endif;
		newPID = newPID + 1;
	endwhile;

	breakpoint;

	currentPID=0;
	while(currentPID < 32) do
		currentPCB=READY_LIST + 32*currentPID;
		if ([currentPCB + 1] == 1) then
			break;
		endif;
		currentPID=currentPID+1;
	endwhile;

	if (currentPID == 32) then
		halt;
	endif;

	breakpoint;

	currentPID=(PTBR - 1024)/8;
	currentPCB=READY_LIST + 32*currentPID;

	alias fileDescriptor S6;
	alias FileIndex S7;
	fileDescriptor=0;
	while(fileDescriptor < 8) do
		FileIndex=[currentPCB + 2*fileDescriptor + 15];

		if (FileIndex == -1) then
			fileDescriptor=fileDescriptor+1;
			continue;
		endif;

		[FILE_TABLE + 2*FileIndex + 1]=[FILE_TABLE + 2*FileIndex + 1]-1;
		if ([FILE_TABLE + 2*FileIndex + 1] == 0) then
			[FILE_TABLE + 2*FileIndex]=-1;
		endif;

		[currentPCB + 2*fileDescriptor + 15]=-1;
		[currentPCB + 2*fileDescriptor + 16]=-1;
		fileDescriptor=fileDescriptor+1;
	endwhile;

	breakpoint;

	[currentPCB + 1]=0;

	alias counter S8;
	alias DataBlock S9;
	counter=0;
	while(counter < PTLR) do
		DataBlock=[PTBR + 2*counter];
		if (([PTBR + 2*counter + 1] == "01") || ([PTBR + 2*counter + 1] == "11")) then
			[MEM_LIST + DataBlock]=[MEM_LIST + DataBlock]-1;
		else
			if (DataBlock >= 448) then
				[DISK_LIST + DataBlock]=[DISK_LIST + DataBlock]-1;
				store(6,20);
			endif;
		endif;
		[PTBR + 2*counter]=-1;
		[PTBR + 2*counter + 1]="00";
		counter=counter+1;
	endwhile;
endif;

if (sysCallNo == 13) then
	alias WaitingPID S2;
	WaitingPID=[physicalSP - 3];

	alias WaitingPCB S3;
	WaitingPCB = READY_LIST + 32*WaitingPID;

	alias currentPID S4;
	currentPID = (PTBR - 1024)/8;

	alias currentPCB S5;
	currentPCB = READY_LIST + 32*currentPID;

	if ((WaitingPID < 0) || (WaitingPID >= 32)) then
		[physicalSP - 2]=-1;
		ireturn;
	endif;

	if ((WaitingPID == currentPID) || ([WaitingPCB + 1] == 0)) then
		[physicalSP - 2]=-1;
		ireturn;
	endif;

	[currentPCB + 1] = 100 + WaitingPID;
	[currentPCB + 2] = BP;
	[currentPCB + 3] = SP-1;
	[currentPCB + 4] = [physicalSP];
	[currentPCB + 5] = PTBR;
	[currentPCB + 6] = PTLR;
	[currentPCB + 7] = R0;
	[currentPCB + 8] = R1;
	[currentPCB + 9] = R2;
	[currentPCB + 10] = R3;
	[currentPCB + 11] = R4;
	[currentPCB + 12] = R5;
	[currentPCB + 13] = R6;
	[currentPCB + 14] = R7;

	[physicalSP - 2]=0;
endif;

if (sysCallNo == 14) then
	alias currentPID S2;
	currentPID = (PTBR - 1024)/8;

	alias newPID S3;
	newPID = 0;
	alias newPCB S4;
	while(newPID < 32) do
		newPCB = READY_LIST + 32*newPID;
		if ([newPCB + 1] == (100 + currentPID)) then
			[newPCB + 1]=1;
		endif;
		newPID = newPID + 1;
	endwhile;

	[physicalSP - 2]=0;
endif;

if ((sysCallNo == 10) || (sysCallNo == 13)) then
	alias currentPID S2;
	currentPID = (PTBR - 1024)/8;
	
	alias currentPCB S3;
	currentPCB = READY_LIST + 32*currentPID;

	alias newPID S6;
	newPID = currentPID + 1;
	alias newPCB S7;
	while(1) do
		if (newPID == 32) then
			newPID=0;
		endif;
		newPCB = READY_LIST + 32*newPID;
		if ([newPCB + 1] == 1) then
			break;
		endif;
		newPID = newPID+1;
	endwhile;

	BP = [newPCB + 2];
	SP = [newPCB + 3];
	PTBR = [newPCB + 5];
	PTLR = [newPCB + 6];
	R0 = [newPCB + 7];
	R1 = [newPCB + 8];
	R2 = [newPCB + 9];
	R3 = [newPCB + 10];
	R4 = [newPCB + 11];
	R5 = [newPCB + 12];
	R6 = [newPCB + 13];
	R7 = [newPCB + 14];

	SP = SP + 1;
	physicalSP = ([PTBR + 2*(SP/512)]*512) + (SP%512);
	[physicalSP] = [newPCB + 4];
	[newPCB + 1] = 2;
endif;

breakpoint;

ireturn;
